import com.bmuschko.gradle.docker.tasks.container.DockerCopyFileFromContainer

import de.undercouch.gradle.tasks.download.Download

import groovy.json.JsonSlurper

import java.time.format.DateTimeFormatter

plugins {
	id "com.avast.gradle.docker-compose" version "0.17.10"
}

File projectDir = project.projectDir as File

class Config {
	public List<String> hotfixURLs = new ArrayList<String>()
	public String namespace
	public List<String> composeFiles = new ArrayList<String>()
	public List<String> composeProfiles = new ArrayList<String>()
	public String databaseName
	public String dataDirectory
	public String liferayDockerImageId
	public int liferayDebugPort
	public int liferayTelnetPort

	@Override
	public String toString() {

		return """

Config:
------------------------
hotfixURLs: ${hotfixURLs}
namespace: ${namespace}
composeFiles: ${composeFiles}
composeProfiles: ${composeProfiles}
databaseName: ${databaseName}
dataDirectory: ${dataDirectory}
liferayDockerImageId: ${liferayDockerImageId}

"""
	}
}

Config config = new Config()

config.composeFiles.add("docker-compose.yaml")

/*
--- BEGIN User configuration
 */

config.composeFiles.addAll([
])
config.composeProfiles.addAll([
	"liferay",

	"mysql",
])
config.hotfixURLs.addAll([
	// "https://releases-cdn.liferay.com/dxp/hotfix/2024.q2.7/liferay-dxp-2024.q2.7-hotfix-4.zip",
])
config.namespace = project.name
config.databaseName = "lportal"
config.dataDirectory = "data"
config.liferayDebugPort = 8000
config.liferayTelnetPort = 11311
config.liferayDockerImageId = gradle.liferayWorkspace.dockerImageId as String

/*
--- END User configuration
 */

static List toList(String s) {
	return s.trim().split(",").grep()
}

def composeFiles = toList(project.getProperty("lr.docker.environment.compose.files"))
List composeProfiles = toList(project.getProperty("lr.docker.environment.compose.profiles"))
String dataDirectory = project.getProperty("lr.docker.environment.data.directory")
String databaseName = project.getProperty("lr.docker.environment.database.name")
List hotfixURLs = toList(project.getProperty("lr.docker.environment.hotfix.urls"))
String namespace = project.getProperty("lr.docker.environment.namespace")

if (!composeProfiles.isEmpty()) {
	config.composeProfiles = composeProfiles
}

if (databaseName != null) {
	config.databaseName = databaseName
}

if (dataDirectory != null && dataDirectory.length() > 0) {
	config.dataDirectory = dataDirectory
}

if (!hotfixURLs.isEmpty()) {
	config.hotfixURLs = hotfixURLs
}

if (namespace != null) {
	config.namespace = namespace
}

ConfigurableFileTree dockerComposeFileTree = project.fileTree(projectDir) {
	include "**/docker-compose.*.yaml"
}

List<String> possibleProfiles = dockerComposeFileTree.collect {
	File file ->

	file.name.substring("docker-compose.".length(), file.name.indexOf(".yaml"))
} as List<String>

List<String> composeProfileFiles = config.composeProfiles.collect {
	String profile ->

	FileTree matchingFileTree = dockerComposeFileTree.matching {
		include "**/docker-compose.${profile}.yaml"
	}

	if (matchingFileTree.isEmpty()) {
		throw new GradleException(
			"The profile '${profile}' does not have a matching docker-compose file. Possible profiles are: ${possibleProfiles}");
	}

	projectDir.relativePath(matchingFileTree.singleFile as File)
}

config.composeFiles.addAll(composeProfileFiles)

tasks.register("exportContainerData", DockerCopyFileFromContainer) {
	containerId = "${config.namespace}-data-helper"

	doFirst {
		mkdir hostPath.get()
	}

	hostPath = provider {
		DateTimeFormatter formatter = DateTimeFormatter.
			ofPattern("yyyyMMdd.HHmmss").
			withZone(ZoneId.systemDefault())

		"exported_data/data_${formatter.format(Instant.now())}"
	}

	remotePath = "/data/container-data"

}
File sourceDataDirectory = project.file config.dataDirectory
File targetDataDirectory = project.file "helper/data"

tasks.register("prepareDataDirectory") {
	doFirst {
		mkdir targetDataDirectory

		copy {
			from sourceDataDirectory
			include "**/*"
			into targetDataDirectory
		}
	}

	if (sourceDataDirectory.isDirectory()) {
		inputs.dir sourceDataDirectory
	}

	outputs.dir targetDataDirectory
}

File sourceDumpsDirectory = project.file "dumps"
File targetDumpsDirectory = project.file "helper/dumps"

tasks.register("prepareDumps") {
	doFirst {
		mkdir targetDumpsDirectory

		copy {
			from sourceDumpsDirectory
			include "**/*"
			into targetDumpsDirectory
		}
	}

	if (sourceDumpsDirectory.isDirectory()) {
		inputs.dir sourceDumpsDirectory
	}

	outputs.dir targetDumpsDirectory
}

tasks.register("prepareHotfixes", Download) {
	dest project.layout.dir(project.provider {project.file("configs/common/patching")})
	onlyIf {
		!config.hotfixURLs.isEmpty()
	}
	src config.hotfixURLs
}
tasks.register("possibleProfiles") {
	description "Displays the possible profile names that can be used with docker-compose."
	doFirst {
		println "Possible profiles: ${possibleProfiles}"
	}
	group "help"
}

clean {
	dependsOn ":cleanPrepareDataDirectory"
	dependsOn ":cleanPrepareDumps"
	dependsOn ":cleanPrepareHotfixes"
}
dockerDeploy {
	dependsOn ":prepareDataDirectory"
	dependsOn ":prepareDumps"
	dependsOn ":prepareHotfixes"
}

composeUp {
	dependsOn ":buildDockerImage"

	doFirst {
		println "Using config: ${config}"
	}

	onlyIf {
		!config.composeFiles.isEmpty()
	}
}

dockerCompose {
	captureContainersOutput = true
	captureContainersOutputToFiles = project.file('containerLogs')

	environment.put "DATABASE_NAME", config.databaseName
	environment.put "LIFERAY_IMAGE_NAME", config.liferayDockerImageId
	environment.put "LIFERAY_DEBUG_PORT", config.liferayDebugPort
	environment.put "LIFERAY_TELNET_PORT", config.liferayTelnetPort
	environment.put "NAMESPACE", config.namespace

	// DEBUG: Set to false to retain volume data on restart
	removeVolumes = true

	tcpPortsToIgnoreWhenWaiting = [config.liferayDebugPort, config.liferayTelnetPort]
	useComposeFiles = config.composeFiles

	// DEBUG: Set to true if container startup is failing
	retainContainersOnStartupFailure = false
}